[← Part two: A very basic Hoon program](2.md) — Part four: Coming soon!

# Urbit By Doing
## Making a talkbot
### Part three: Connecting to talk

> Social media is not about the exploitation of technology but service to community.  
-- *Simon Mainwaring*

Our program can take some input. That's cool and all, but it's currently not very useful. A talkbot should operate on its own, but it'll still need some manual setup. In this part, we're going to try and make that manual work as easy as possible for us. We'll define different "commands" for our talkbot application and use them to join and leave talk stations. By the end of this, we'll be able to look at the data talk produces for our sandbox channel!

Looking for the code you'll end up with? Find the Hoon file for this part [here](3.hoon).

#### Tagged unions, they're It!

We'll be implementing three "administrative" commands for our talkbot. "Join" and "leave" will both require us to specify a ship and channel pair, but "joined" (which we'll use to see the stations our talkbot is currently monitoring) won't require any data. We'll need a way to differentiate between these different kinds of inputs and their intended meaning. Luckily, there's a fancy way to do that!

Consider the following mold: `{$tag @}`.  
And look at a matching noun: `[%tag 42]`.  
That `%` notation, doesn't it look familiar? We used that to tag the data in our debug prints! `%tag` is a string constant, whose mold ("type") is `$tag`. If we pair that with a noun, we get a so-called tagged union. They're super useful for differentiating between different kinds of data. I could write a couple more paragraphs of explanation right now, but I can cut that down if I have an "in the wild" example to refer to, so let's build one right now!

In our talkbot's code, below the `!:`, type the following few lines:

```
|%
++  action
  $%  {$join s/station:talk}
      {$leave s/station:talk}
      {$joined $~}
  ==
--
```

So, what does that mean, and what does it do?

`|%` is a core rune, just like the `|_` we've already used, so we know the `--` at the end closes it. This kind of core is commonly used for defining molds ("types") because, unlike the `|_` core it doesn't take any parameters (which we don't need since all we're doing is laying down some definitions).  
Why do we not just stick our mold definition arms in the `|_` core below, together with our code? By defining them ahead of time, we can compute the default values (called bunts) for our molds at compile-time. Much nicer than computing that every time it is needed!

`++  action` opens an arm, like we've done before. This time though, we're not attaching the name to a gate, but to a mold!

`$%` is used to construct a mold which recognizes a tagged union. Since this rune takes a list of things, we close it with `==`. If you managed to follow the description of tagged unions above, you may already begin to see what's happening here. If not, that's fine, we'll walk through it!  
Essentially, the three different molds we're defining here are options for what an `action` can be. If an action's first element is `%join` or `%leave`, we know the second element is of mold `station:talk` and is accessible through the face `s`. For example, if we have this data: `[%join somestation]` stored in `myaction`, we can do `s.myaction` to retrieve the station.  
Regarding the data in our actions, `station:talk` is a mold we're borrowing from talk itself, more on that in a bit. `$~` is the type for `~` (null), so we're saying we expect no data for a union tagged with `%joined`. We'll still have to specify the `~` when we eventually construct the `joined` action though, because a tagged union without data would just be a tag.

#### Hey neighbor, can I borrow a cup of mold?

So, we got all that fancy-looking new code, but it doesn't compile! If you've saved your code, you'll see an error containing `-find.talk` show up in your Urbit's console, meaning the compiler couldn't find this `talk` thing we're talking about, or isn't sure it even exists to begin with!  
Luckily, talk has defined its data structures in a dedicated file, which we can "include" by adding the following line *above* the `!:` in the code.

```
/-  talk
```

All the `/` runes are not really runes, they're more like instructions for Urbit's build system. What `/-` does is look in your `sur/` folder for, in this case, `talk.hoon`, and "include" whatever is defined there. If you take a look inside that file, you'll see quite the list of molds, like `audience`, `report` and `station`.

That last one's important, because it's what we'll be using in our program today! You'll see it's defined as `(pair ship knot)`. A `ship` is a ship address, `@p`, like `~dozbud`. A `knot` is a string as a `@ta` (ASCII text), whose value we'd write as `~.somestring`. They're wrapped in a `pair`, which means we get to address the first part (the `ship`) as `p`, and the second part (the `knot`) as `q`. So if we wanted to get the ship from some `station`, we'd say `p.somestation`. Together, that data makes a talk station: a channel on a ship.

#### Smooth moves

Now that we made our own mold and have a rough idea of how those things work, we'll want to take another look at that `++  poke-noun` we wrote in the last part. We're going to completely change that up in a little bit, but one thing that won't change is the `^-` line. We'll still be producing that list of moves. But... what is a `move`? Or rather, what can it be? Though the OS expects them, it leaves us without a clear definition of what a move looks like. Rude. We'll have to define the molds for the different kinds of moves we want to use ourselves (so that the type-checker can do its thing and point out errors for us), so let's put the following inside our `|%` core:

```
++  move  {bone card}
++  card
  $%  {$peer wire {@p term} path}
      {$pull wire {@p term} $~}
  ==
```

We won't be covering the above in very much detail, since "this is just how it works", and we'll be explaining the code that uses these molds later on anyway. Still, to satisfy your potential appetite for information, here's a quick rundown of the unfamiliar terms:

`bone` is a number that's used to identify the source of an event, you could compare it to a socket descriptor for literally any interaction with your application.  
The definition of `card` looks somewhat similar to our `action`, it has that whole tagged union thing going on. Both kinds of `card` have a `wire`, which looks like a path and is used as an identifier.  They also share `term`, an application's name. The `path` used for `%peer` cards is, well, a path.

#### Get on the case

Recall what functionality we want to add to our talkbot today: the ability to join and leave stations, and see which stations we are currently in. An obvious requirement for this is that we want to be able to store that list of "currently joined stations" in our application's state. Fulfilling that is as simple as changing the argument to our `|_` core:

```
|_  {bowl joined/(list station:talk)}
```

Just like that, we have a useful variable in our state! With all necessities accounted for, we can finally change our `++  poke-noun` to do something useful. Let's build it up from scratch, bit by bit.

```
++  poke-noun
  |=  a/action
  ^-  {(list move) _+>.$}
```

Not much has changed there, except that the arm will now see whatever noun we poke it with as an `action`.

```
  ?-  a
  {$join *}
    ::TODO  Join a station.
  {$leave *}
    ::TODO  Leave a station.
  {$joined $~}
    ::TODO  Print stations we've joined.
  ==
```

Hey look, a new rune! It's one of the many `?` runes, which usually branch on a logical test. This particular rune, `?-`, is like a switch statement without a default case. The argument to our switch, `a`, is compared against the three molds, and we branch into the matching case. You'll notice there a case for every union in our `action` mold, but they aren't exactly the same. Since we already know an `action` tagged with `%join` contains a `station:talk`, we don't need to match on that anymore, so we can just say `*` for noun, any data. This keeps the cases flexible. For `%joined` we'll still need to specify we expect null though.  
Let's write the code for those three cases, one at a time.

As a quick side-note, you may be wondering how Hoon knows where branches end if we don't have any delimiters for them. Like we've seen with non-branching arms already, branches end where they produce something. It's like a return, without the explicit return keyword.

#### Joining the party

```
  {$join *}
    ?^  (find [s.a ~] joined)
      ~&  [%already-joined s.a]
      [~ +>.$]
    ~&  [%joining s.a]
    :-  [[ost %peer /listen/(scot %p p.s.a)/[q.s.a] [p.s.a %talk] /afx/[q.s.a]/(scot %da now)] ~]
    +>.$(joined [s.a joined])
```

Hey look, another `?` rune! We already know this one branches, but on what exactly? `?^` check if its first "argument" (the call to `find`) is null. If it isn't, it goes into the first branch. If it is, the second. So basically, it's an "if not null".

So what do we actually check for null? In Hoon, "functions" are called as `(func arg)`, and you can obviously supply any number of arguments if your "function" so desires.  
Here, we call `find`, which searches for the first occurrence of a list in another list. We can use this to search for a single element as well, we just have to turn it into a list, or something that's "close enough". Lists are null-terminated, `[s.a ~]`. This is enough for `find` to be able to work with it as if it were a list. Notice how we can ask for the `s` in `a` because we know our `action` is a `%join`, and as such is guaranteed by the type-checker to have that `s` available, like we defined under `++  action`.  
The second argument, `joined` is simply the list we want to search through. That's right, we don't need anything fancy to reference to our application state. We're inside the `|_` that defined it, after all.
Find produces (returns) a so-called unit of an atom, the index of the found element. A unit is either null, or a pair of null and a value. More on that in a little bit.

If `find` manages to find the station we want to join in the `joined` list, it means we've already joined that one. We use `~&` to print out a message to inform us of that, and then produce that "empty list of moves, unchanged state" thing.

If it came back with null, that gives us the a-okay to join that station. We use `~&` again to inform the user of this success, and then use `:-` to construct our output. What `:-` does is construct a cell (pair or two nouns) of its two "arguments". For example, `:-(1 2)` (hey look, wide-form!) would produce `[1 2]`. Why don't we just type the latter? In this case, both nouns are rather long, so we use `:-` to break it up into multiple lines. Another cool use-case is to say "we already know the first noun, but the second noun requires some more logic to figure out". That way, you get to state the first noun only once, instead of repeating it every time you're finally certain of your output. Example pseudocode:

```
:-  'certain'
?:  condition
  'option 1'
'option 2'
```

As you get more familiar with all the runes, you'll learn that you need very few of them to write useful code, and that a lot of runes are just there to help make your code more elegant and pretty.

Anyway, what are we actually putting into our cell? We know it has to be a `{(list move) _+>.$}`, so let's break down those two components: the list and the state thing.  
(Remember, `station:talk` is a pair of `ship` and `knot` (channel name), so we can use `p` and `q` to access those respectively.)

From the outside-in, we have `[a-bunch-of-stuff ~]`. All that stuff it wrapped in a cell with a trailing `~` to turn it into a list (with a single element). As you might be able to guess already, the stuff is our `move`.  
It starts with `ost`, which contains a number our application uses to identify itself (as an event source). Don't worry too much about it.  
Next is `%peer`, a tag you may recognize from our definition of `card`. When you send a peer move, it subscribes your application to the data another application broadcasts on a specific "address".  
Starting from `/listen` is the wire for our subscription, a path-like thing we use to identify this particular subscription whenever we get data from it. We use `scot` to convert the planet of our station to a string to add to our wire, using `%p` to tell `scot` it needs to interpret our data as a planet. We also stick the channel name at the end of our wire, and since it's already a string, we don't need to convert it.  
`[p.s.a %talk]` defines the source we want to subscribe to. The station we'd like to join is running on the station's planet, so it's only appropriate we ask *that* planet's talk to share its data with us.  
Lastly, there's the wire on the other application (that foreign talk) we want to subscribe to. It starts with `/afx`, then mentions the channel's name, and closes up with the current time. Why the current time? We want all messages from this point onward, none of the messages before it. Don't want our talkbot to chat with ghosts of the past, right?

As for the second part of our product, `+>.$(joined [s.a joined])` says "take all current state data, with `joined` changed to that new value", where that new value is our list, `joined`, with `s.a` prepended to it. We'd append, but there's a trailing `~` there which we'd need to work around, so this is easier. How I just described it isn't the full story (you can also use similar constructions to do loops, and so much more!), but practically speaking that's what we're doing here.

It's perfectly fine if you're not entirely comfortable with all those details yet. It'll probably start making more sense after you've seen those things in a couple of different use cases, and played with them a little yourself. For now, let's keep going. Most of what we just learned will be re-applied in the `%leave` case, so we should be able to go over that one a bit quicker.

#### Leave no case behind

```
  {$leave *}
    =+  i=(find [s.a ~] joined)
    ?~  i
      ~&  [%already-left s.a]
      [~ +>.$]
    ~&  [%leaving s.a]
    :-  [[ost %pull /listen/(scot %p p.s.a)/[q.s.a] [p.s.a %talk] ~] ~]
    +>.$(joined (weld (scag u.i joined) (slag +(u.i) joined)))
```

Lots of things here we've already seen in our `%join` case, but there's a few new elements. Let's go over those!

`=+` is one of the `=` runes, the only group aside from cores (`|` runes) that have the power to change the subject. Effectively, this means they can add or change variables in your program. `=+` is the simplest way of defining a variable. In our case, we assign the result of our `find` call to `i`.

In the case of wanting to leave a station, we can only do that if we are already in it. We use `?~` to test `i` (the result of `find`) for null. We did that above, too, with `?^`, so what's different? The branches are in a different order. `?~` executes the first branch if the "argument" is null, the second if it isn't. Like an "if null". By using this instead of the "if not null" rune, we can keep our code as flat as possible.  

We again construct our product with `:-`, for the same reasons as explained above. Its contents are different though. We send a `%pull` move this time to say we want to *un*subscribe. We pass in a wire identical to the one we declared when we first subscribed, so the system knows what subscription to pull. We also specify the talk running on that foreign ship again. Those two pieces of data are enough for `%pull` to do its job.

This time, we don't want to add an item to our list, we want to remove one instead. This is why we stored the result of `find` in `i`. Now remember, that result is "the unit of an atom". We already checked if that unit was null, and if we made it to this point, we know it isn't. That means it's value is something like `[~ index]`, but how do we get at that index? For all units, you can retrieve their value (if you know the unit isn't null) by referring to the `u` face. Hence why you'll see `u.i` instead of just `i`.  
The Hoon standard library doesn't have a function to delete a specific element from a list, so instead we have to surgically remove it ourselves. `scag` takes an atom `a` and a list, producing the first `a` items of that list. `slag` also takes an atom `a` and a list, producing the remaining items of that list starting at index `a`. We can use those to take all items before and after the station we just left (`+(u.i)` is just `u.i` plus one), and we concatenate those two lists using `weld`, which does exactly that.

#### Almost there!

```
  {$joined $~}
    ~&  [%currently-joined joined]
    [~ +>.$]
```

Well that was just too easy. Since our program knows `joined` is a list of stations, it can print it as such. It's not the prettiest, sure, but it fulfills its purpose: being informative.

One last piece of code we want to add won't be fully explained until the next part, but we'll put it in now because it keeps our program from producing errors whenever it receives data. This also has the added benefit of showing us that what we wrote today actually works! Add the following new arm to the `|_` core:

```
++  diff-talk-report
  |=  {wir/wire rep/report:talk}
  ^-  {(list move) _+>.$}
  ~&  %got-talk-report
  [~ +>.$]
```

You should be able to tell what this code does by now: nothing much. This arm will get called whenever we receive a `talk-report`, which is what talk sends out to its subscribers. That little `~&` will let us know if it's working properly!

Assuming you've already set up your sandbox talk channel (see [part 1](1.md)), execute the following into dojo:

```
:mytalkbot [%join our ~.sandbox]
```

Conveniently, `our` contains our ship's name, so we can use that since the station is running on our ship. `~.sandbox` is the notation for a `knot`/`@ta` (ASCII text) that contains the string "sandbox". It might have been neater to put square brackets around those two parts of the `station:talk`, since they're part of a whole, but because of how cells work (`[1 2 3]` is `[1 [2 3]]`) we don't really need to.

Some output will fly by, and already a couple of `%got-talk-report` messages will appear. That's talk sending us info on the station we just joined! Now, switch to talk's interface with `ctrl+x` and send something to your sandbox channel. If all went well, a `%got-talk-report` should pop up along with your message!

You can send the `%leave` action in the same way, or do `[%joined ~]` if you want to check the output it gives.

#### Side-notes on style

There's a couple things I want to say about good Hoon code style, now that we have something worthy of being called a codebase.

It's generally good practice to keep your code as "flat" as possible, having it use the least amount of indentation it can while still keeping things clear and neat. Remember how we used `?^` and `?~` to branch on our "failure" condition? We used those specific runes there so that we could indent our early-exit situation, and then keep the else, our "main" code, on a more shallow indentation. Sticking with this avoids indentation creep, which keeps your code much neater. (Of course there are exceptions, never sacrifice readability. But usually, there's a rune that can help you make things look good.)

Regarding naming conventions, you'll see really short, often un-descriptive faces (variable names) in most of the standard Urbit hoon code. Here, we used `a` for "action" because it's more or less the only variable we used in our arm. It's clear enough. But again, never sacrifice readability. If you need a twenty-letter face to make your code understandable, so be it.

If you look at the source code provided for this part ([3.hoon](3.hoon)), you'll see I added some empty lines here and there, for spacing. Officially, empty lines are a no-no and should be lines with just a `::` on them, but luckily this isn't compiler-enforced (and nobody is paying me to write this), which means it's up to personal preference. If you ever plan on contributing to Urbit's own codebase though, you'll need to get into the habit of not leaving lines empty, ever.

Lastly, some of our code (the move we produce for the `%join` case) goes over the recommended 80-character maximum width. We could have broken it up a bit more to prevent that, but it's not all that big a deal. Again, the Urbit codebase does expect your lines of code to stay within 80 characters, so keep that in mind.

#### Try me!

If you're brave, try your hand at this exercise!  
We can tell our talkbot to leave individual stations, but sometimes it may be useful to have it leave every station it's joined. Try making a command for that! Add `{$leaveall $~}` to the `action` mold and implement functionality for it. Whether you succeed or fail, [email me](mailto:fang@fang.io) your questions and/or code, or post them to `urbit-meta`. We're always happy to help, and would love to see what you came up with!  
(I put a link to a Hoon doc page down below to help you on your way!)

#### Next time

In the next part, we'll teach our talkbot how to look through those talk-reports we're getting, have it respond to messages and all that. That's right, actual functionality. It's going to be fun!

#### Relevant reading

[Quickstart: Network messages](http://urbit.org/docs/arvo/network/)  
[Quickstart: State](http://urbit.org/docs/arvo/state/)  
[Quickstart: Subscriptions](http://urbit.org/docs/arvo/subscriptions/)  
[Hoon: Standard library: list logic](http://urbit.org/docs/hoon/library/2b/)

[← Part two: A very basic Hoon program](2.md) — Part four: Coming Soon™!
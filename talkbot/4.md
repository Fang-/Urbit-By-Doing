[← Part three: Connecting to talk](3.md) — Part five: Coming soon!

# Urbit By Doing
## Making a talkbot
### Part four: Listening and responding

> Looking ahead, future generations may learn their social skills from robots in the first place.  
-- *Daniel H. Wilson*

We now have this `diff-talk-report` function that receives talk data, so how about we start doing something with it? Let's get our bot to respond when it's greeted. In doing so, we'll learn a lot about this mysterious `talk-report` data structure.  

Looking for the code you'll end up with? Find the Hoon file for this part [here](4.hoon).

#### Reported for talking

Our `diff-talk-report` arm gets called whenever talk pushes data, we know that much. But why? If an application wants to push data to its subscribers, it does so by sending a `%diff` move. The application can also specify what kind of data it is, so its subscribers can handle it appropriately. In this case, we want to react when talk publishes a `talk-report`, so we name our arm to reflect that. If we wanted to program functionality for when talk published an atom, we would've used `diff-atom` instead.

Again we must ask: why? Why the `report`, and what kind of data structure is it? Recall how we included `sur/talk.hoon` using `/-`. We said it contains a bunch of molds for talk-related use, so it may just contain the `report` as well. If we go to investigate, we'll find the following:

```
++  report                                              ::  talk update
  $%  {$cabal cabal}                                    ::  config neighborhood
  ::  {$folder (list report)}                           ::  multiple
      {$grams (pair @ud (list telegram))}               ::  beginning thoughts
      {$group register}                                 ::  presence
      {$house shelf}                                    ::  station set
      {$glyph (jug char (set partner))}                 ::  relevant binding
  ==  
```

It's got comments, how convenient! Sadly they don't tell us all that much yet, but we can already see that the report is a tagged union. All the data in those unions is pretty vague though, it's just more of talk's molds. "Beginning thoughts" is the most message-sounding of the bunch, so let's delve into that one. We can already see it comes with an unsigned decimal (`@ud`, it's the message's local number) and a list of telegrams. What are those about?

```
++  telegram   (pair ship thought)                      ::  who which whom what
++  thought    (trel serial audience statement)         ::  which whom what
++  serial     @uvH                                     ::  unique identity
```

Okay, that's easy enough. A telegram has a "sender" and a "contents", in the form of a ship and a thought. The contents are a triple of some "unique identity", an audience, and a statement. If you know your talk usage terminology, you'll recognize "audience". It's the recipients of a message. Its mold reveals:

```
++  audience  (map partner (pair envelope delivery))    ::  destination+state
++  partner   (each station passport)                   ::  interlocutor
```

We're just going to ignore the map's values for now, since it keeps things simpler. The partner is interesting though, it's an `each`, which we haven't seen before. It's essentially "either A or B", indicated by a loobean (boolean) tag. If it's `$&` (aka `%.y`, "true"), the `each`'s value is of mold A (`station`, in this case). If it's `$|` (aka `%.n`, "false"), its value's mold is B (`passport` here). We already learned about talk's station mold in the previous part, and passport isn't really relevant right now, so we know everything we need about audiences now!

```
++  statement  (trel @da bouquet speech)                ::  when this
++  bouquet  (set flavor)                               ::  complete aroma
++  speech                                              ::  narrative action
  $%  {$exp p/@t}                                       ::  hoon line
      {$url p/purf}                                     ::  parsed url
      {$lin p/? q/@t}                                   ::  no/@ text line
  ==       
```

So a statement has a timestamp (`@da`), some weird smelly thing (again, not important for us right now), and a kind of message. There's a whole bunch of other unions in `speech`, but these three are the ones you've likely already seen in your day-to-day use of talk. They should be fairly self-explanatory by now, except for maybe the `p/?`. `?` is a loobean, which we detailed in the previous paragraph.

So now we know, more or less entirely, what a talk-report containing messages looks like. It's not the simplest structure, but it does give us a lot of information to work with. Before we get to that though, let's see what it takes to actually read individual messages.

We're going to write a more functional `diff-talk-report` function to process the talk reports. A separate function, `read-telegram`, will be used to process the messages we find in those reports. We'll also be writing a `station-from-wire` function for figuring out the `station:talk` a message came from. Let's go!

#### A message, for me?

```
++  diff-talk-report
  |=  {wir/wire rep/report:talk}
  ^-  {(list move) _+>.$}
  ?+  rep
    ~&  [%unprocessed-report-type -:rep]
    [~ +>.$]
  {$grams *}  ::  Message list.
    ::TODO  Process a list of messages.
  ==
```

That's slightly longer than the temporary `diff-talk-report` we whipped up in the last part. As usual, we're moving bit by bit.

The first two lines are the same we used in the temporary version of this arm, and you can probably guess what they mean. We expect two arguments: a `wire` and a `report:talk`. Again, the wire's for identifying the subscription that sent us this `report`.  
As is the case for most "OS-invoked" arms, we want to produce a list of moves and a new state. Business as usual.

`?+` is a new rune, but strikingly similar to the `?-` we learned about in the last part. Where `?-` doesn't do default cases (and thus expects all potential cases to be covered), `?+` is a bit more easy-going. The first bit of code we give it is the default case, and we can then go on and match any number of possible cases we please. Of course, we still end our list of cases with `==`.

For the default, we print out the tag of the `report`, i.e., the kind of report it is. `-:rep` says "get the very first thing in `rep`, whatever either of those are. It's not good practice, but this is purely informational, so it's okay. You can leave this line out if you're not interested in it.

As we discovered in our research above, reports that contain actual messages are tagged with `%grams` (for "telegrams"). Those are the only ones we want to work with right now, so they get their very own case! Here's what "processing a list of messages" looks like:

```
    :_  +>.$
    %+  murn  q.rep
      |=  gram/telegram:talk
      (read-telegram wir gram)
```

(If you tried your hand at the previous part's exercise, your result may look something like this code. Other solutions would also be correct of course, as long as they work!)

`:_` is a cell rune. Like `:-`, we use it to construct and produce a cell. But where `:-(a b)` makes `[a b]`, `:_(a b)` makes `[b a]`. In other words, it's inverted! Useful for when, like this case, the first part of the cell is more complex than the second. We don't want to change state after processing messages (yet), so we just produce the current state, unchanged, for the state part of our product.

To fill in the first part of our product, the list of moves, we make a rather lengthy call to `murn`. To help keep things pretty and readable, we use the `%+` rune, which we can use to call a function that takes a pair sample, meaning two arguments. Instead of `(func a b)` we get to write `%+(func a b)`. In wide-form that doesn't have any extra value, but in tall-form we get to spread things out over multiple lines, as you can see in the code.

Now, what does `murn` do? It is a bit like a "try for each" for lists. It takes a list as its first argument, and a gate (function) for the second. It expects the gate to produce a `unit`, and throws every element from the list into that gate, one by one. It compiles all produced non-null values into a list, and spits that out.

As we saw, a `%grams` reports contains a `(pair @ud (list telegram))`. We want to get at that list of telegrams, so we get the second pair element from our report: `q.rep`.  
Looking at the gate we make up on the spot there, we can see it does indeed take elements from our list, of mold `telegram:talk`.  
`read-telegram` is a function we're going to write to process individual messages for us, and along with the message `gram`, we're giving it the wire `wir`, so it too knows where the message originated from. (Actually, the telegram itself also contains that information, but just using the wire is a tad simpler in some cases.) We expect a `(unit move)` out of it to conform to `murn`s expectations and build up a list of moves, which is what we want in `diff-talk-report`s product.

#### On the wire

Remember how, when we set up subscriptions, we specified a wire in the form of `/listen/ship-name/channel-name`? We're going to want a way to get that information out of the wire again, so we know the station any data we might get came from. We'll do that by implementing `station-from-wire`.

But, why don't we just look at the audience of the message to deduce the station we should send our reply to? We've seen that `thought`s contain that information. Well, it's possible for messages to be sent to multiple recipients, who are all part of the audience, so we'd still not know which one to send our reply to. By looking at the wire (identifier) of the subscription that got us the message, we can be 100% certain of the correct place to put our response.

```
++  station-from-wire
  |=  wir/wire
  ^-  (unit station:talk)
  ?:  ?=({$listen *} wir)
    $(wir t.wir)
  ?.  ?=({@tas @tas *} wir)
    ~
  =+  ship=(slaw %p i.wir)
  ?~  ship
    ~&  [%unparsable-wire-station wir]
    ~
  =+  channel=(crip (slag 1 (spud t.wir)))
  [~ [u.ship channel]]
```

You'll start to see some of this code is starting to look familiar. The first two lines say we take a `wire`, and will be producing a `(unit station:talk)`. We produce a `unit` of a `station`, just for those weird cases where we can't deduce a station from the wire.

But look here, a bunch of new runes! Surprisingly, we haven't covered the simplest `?`-rune of them all yet: `?:`. It branches on a condition, so it's just a simple `if`. The `?.` below it is the reverse, like `if not`.

There's one more new rune here: `?=`. We use its product as our condition, but what does it mean exactly? `?=` is essentially a pattern matcher. It tries to see if the value of the second argument fits in the mold of the first. (Fun fact, this is also what our switch runes, `?-` and `?+` use under the hood!) For example, we first try to see if our wire `wir` starts with the symbol `listen`, followed by a noun, `*`, literally anything. Later, we try to see if the wire `wir` contains *at least* two more symbols.

That still doesn't explain *why* we do those checks though. If our wire starts with `listen` (which is exactly how we set it up) we know the data we want is beyond the first part of the wire. We then call `$`, which refers to our current core (the nearest `|`-rune, our gate/function `|=`), with the value of `wir` changed to `t.wir`. Yes, that's recursion! You'll see similar things done when you start writing loops.  
To expand a bit on the meaning of `t.wir`: wires are literally a `list` of symbols (constants), just like a `path`. Defining a wire `/a/b/c` will get you the list `[%a %b %c ~]`. Since cells right-associate and `list`s use faces `i` and `t` for head and tail respectively, that gets you `[i=%a t=[i=%b t=[i=%c t=~]]]`. Since we know the first element is `%listen` in our case, we "discard" it by recursing into `station-from-wire` with the value of `wir` set to the wire beyond its first element, namely `t.wir`.

Once we know our wire has two hopefully useful symbols for us, we can get to work on them. To get a ship from the first part of the wire, we call `slaw` and tell it we need an `@p` by specifying `%p`. `slaw` isn't guaranteed to succeed, so it returns a `unit`, of which we need to check wether it's null. If it is, our parse failed and we have an incompatible wire on our hands.  
For the channel, we do something a tad different. We use `spud` to convert the second part of the wire straight into a `tape` (list of characters). This includes a leading `/` though, so we use `slag` to take everything on and after the first index in that list, then convert it to a `cord` (`@t` string) by calling `crip` on it. Long story short: we get the channel name of the station.

(Note that our failure cases will likely never happen, because of how we set up our subscriptions and their wires.)

Now that we have an accurate way to get the target of any responses we'll want to send to a message, we can finally get to work on reading them!

#### Dear ~mytalkbot,

Let's implement `read-telegram`, which will be called for every `telegram` we receive.

```
++  read-telegram
  |=  {wir/wire gram/telegram:talk}
  ^-  (unit move)
  =+  aud=(station-from-wire wir)
  ?~  aud
    ~&  %message-source-unclear
    ~
  =*  msg  r.r.q.gram
  ?+  msg
    ~&  [%unprocessed-message-type -:msg]
    ~
  {$lin *}  ::  Regular message.
    ::TODO  Process regular messages here!
  ==
```

The first couple of lines we already talked about. We expect a wire and a telegram, and maybe we produce a move.

As discussed above, we use `station-from-wire` to get, well, the station from the wire that belongs to the subscription that sent us our message. There's eventually going to be many different branches of code that want to send a response, and they'll likely all be sending it to the station we got the message from, so we store `aud`, the target audience, now rather than later. Avoid repeating ourselves, as we should.

Below that we have a new rune to help make our lives easier. `=*` defines an alias, in this case saying that `msg` refers to `r.r.q.gram`. What does the latter mean? If we look back at the structure of a `telegram`, we see it has a `thought` as its second element, which has a `statement` as its third element, which has a `speech` as its third element. Remember, `[p q]` for `pair`s (tuples), `[p q r]` for `trel`s (triples). That `speech` is the message content we want to work with, and writing down the long `r.r.q.gram` is going to get old really fast, so we alias it as `msg`. Comfy!

The next bit of code is commented out, because it's functionality we don't currently need. What's more, it may hinder us in the testing of our talkbot! What is does is check to see if the telegram's first element, its sender, is us, `our` ship. If it is, we want to ignore it, so we produce null.  
If you're following this tutorial to a T you'll eventually be testing by posting in your sandbox channel from the same ship you're running the talkbot from. Ordinarily, we'd prevent the talkbot from responding to itself to keep feedback loops from happening, but since you and the talkbot will be posting under the same name (from the same ship), we want it to forget about that safety measure for now.

Below that, we have another `?+` switch, again with a default to notify us of message types we're not dealing with. We looks at `msg` and try to match on its tag. We specify whatever comes after the tag as a generic noun `*` because here we don't need to specify what exactly it contains. As long as it matches the tag, we and the compiler know what else there is, thanks to the way talk has defined the `++speech` mold.

Then, we finally get to the processing of messages! For now we just deal with `%lin`s, regular text messages. We convert the message contents to a `tape` using `trip`, because strings as lists of characters are easier to work with than strings as large numbers.  

When we start thinking about processing messages and sending responses, we quickly run into a pretty big question: how is our bot going to send a message? Let's work on that!

#### I command you to talk.

To send a talk message, we'll need to send a move. That's great, but what does such a move even look like? Our program doesn't know, so we need to tell it. Remember that definition of `move` we have at the top of our program? Add the following kind to it:

```
{$poke wire {@p term} {$talk-command command:talk}}
```

It's a `%poke` move, which are used to — you guessed it — poke applications! This one, just like the others, has a `wire` and a `{@p term}` to specify our target application with. Beyond that is a newcomer, a `%talk-command`. You can check that `sur/talk.hoon` file for its definition of `command`, but we're just going to take all this for granted.

Now that we have everything we need defined, let's get to implementing `send`.

```
++  send
  |=  {aud/station:talk mess/?(tape @t)}
  ^-  move
  =+  mes=?@(mess (trip mess) mess)
  :*  ost
      %poke
      /repeat/(scot %ud 1)/(scot %p p.aud)/[q.aud]
      [our %talk]
      (said our aud %talk now eny [%leaf (weld ":: " mes)]~)
  ==
```

We can easily recognize now it takes a station and a message as input, and intends to produce a `move`. But what's with the type of that message, `?(tape @t)`? It's shorthand for the `$?` rune, which says it can be any one of the molds (types) passed in. Why do we do this? We don't want to worry about the kind of string we pass in every time we call this function, so we just take both.  
That does mean we have the responsibility to convert it to what we need, which happens to be a `tape`. We use the `?@` rune to check if our argument is an atom, which in this case would mean it's an `@t`. If it is, we use `trip` to convert it to a `tape`. If it isn't, well, we got what we need already.

Next, we build our move! For this we use the `:*` rune, which constructs a cell with as many elements in it as we pass. We close that list of elements with `==`, like we do with any rune that takes a list of arguments.  
`ost` as our app's unique identifier, `%poke` is the kind of move we're making, a wire to identify it, and a target application. Don't worry about the structure of the wire, it's just how talk (our target app) expects outgoing messages to be labeled.

...Wait a second, what does `said` do then? We can see it uses `weld` (which concatenates two lists) to prepend ":: " to our message, which will help it stand out as something sent by a bot, but what does all that other junk mean? We haven't even defined `said` anyway.   
If we take a peek into the file `lib/talk.hoon`, we will find a function called `said`. We'll be copying and slightly modifying that for our purposes.

```
++  said  ::  Modified from lib/talk.hoon.
  |=  {our/@p cuz/station:talk dap/term now/@da eny/@uvJ mes/(list tank)}
  :-  %talk-command
  ^-  command:talk
  :-  %publish
  |-  ^-  (list thought:talk)
  ?~  mes  ~
  :_  $(mes t.mes, eny (sham eny mes))
  ^-  thought:talk
  :+  (shaf %thot eny)
    [[[%& cuz] [*envelope:talk %pending]] ~ ~]
  [now *bouquet:talk [%lin & (crip ~(ram re i.mes))]]
```

That's... a little overwhelming, isn't it? There's a lot going on here, and explaining what all this does and why it does it will take too many words for the remainder of this part. If you're gutsy and want to try to understand this, start by looking up the new runes here, then try and see if you can match what is being constructed here with some of the types defined in `sur/talk.hoon`. If you want to know what's being done with all that data, you'll have to delve into talk's source code in `app/talk.hoon`.

Meanwhile, we're just going to take this for granted, and rely on the `send` function we wrote to "just work", regardless of the things it calls upon.

#### Echo, :: you said: echo

With all the groundwork done, we can *finally* get to sending replies to people! Let's fill in that to-do we put in the `$lin` case in `read-telegram` earlier.

```
  {$lin *}  ::  Regular message.
    =+  tmsg=(trip q.msg)
    ?:  =((find ":: " tmsg) [~ 0])
      ~
    :-  ~
    (send u.aud (weld "you said: " tmsg))
```

First of all, we use `trip` to convert the message contents to a `tape`, since a list of characters is easier to analyze than a number.

Next, we use `find` to see if we can find something in that list. `find` returns a `(unit @)` representing the starting index of the first result it found, if anything. We check to see if the message starts with ":: ", and ignore it if it does. We want to ignore messages from bots, to avoid getting into loops.

If all that is good, we can construct our `(unit move)`, with leading `~` and the move generated by a call to `send`. For now, we simply echo the message we got back, using `weld` to prepend "you said: ".

Now, save your code, pray it compiles, and switch over to talk in the CLI. Send a message. If your bot has joined the channel you're talking in, it will repeat your message back to you!  
(If you're using your bot's comet to send messages into its sandbox channel, you might notice its replies appear before the message you sent does! That's a side effect of messaging from the bot's ship.)

```
~palfun-foslup= echo
~laddyt_sidpub= :: you said: echo
```

#### Try me!

Have your bot respond when called by its ship name.  
Again, whether you succeed or fail, [email me](mailto:fang@fang.io) your questions and/or code, or post them to `urbit-meta`. We're always happy to help, and would love to see what you came up with!

#### Next time

In the next part, we'll be teach our bot some new tricks, like doing HTTP requests!

#### Relevant reading

[Hoon: Standard library](http://urbit.org/docs/hoon/library/)  
[`/sur/talk.hoon`](https://github.com/urbit/arvo/blob/master/sur/talk.hoon)  
[`/app/talk.hoon`](https://github.com/urbit/arvo/blob/master/app/talk.hoon)

[← Part three: Connecting to talk](3.md) — Part five: Coming ~~Soon~~ never ever! Sorry!
